plugins {
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version "${kotlinVersion}"
}

mainClassName = 'io.github.sammers21.tacm.server.MainKt'

dependencies {
    compile project(":core")
    compile group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:${jacksonKotlinModuleVersion}"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

jar {
    into('webroot') {
        from("$rootDir/http-server/webroot").collect()
    }
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest {
        attributes 'Main-Class': mainClassName
    }
    archiveBaseName = "http-server"
}

task copyWeb(type: Copy) {
    from "$rootDir/frontend/build"
    into "$rootDir/http-server/webroot"

    doLast {
        println "Frontend has been copied"
    }
}

task buildStaticFront(type: Exec) {
    workingDir "$rootDir/frontend"
    commandLine 'npm', 'run', 'build'
    standardOutput = new ByteArrayOutputStream()
    ext.output = {
        return standardOutput.toString()
    }
    doLast {
        println standardOutput.toString()
        println "Frontend has been built"
    }
}

tasks.copyWeb.dependsOn(buildStaticFront)
tasks.jar.dependsOn(copyWeb)