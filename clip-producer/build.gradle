import org.ajoberstar.grgit.Grgit

plugins {
    id 'java'
    id 'application'
    id "org.ajoberstar.grgit" version "4.0.2"
}

mainClassName = 'io.github.sammers21.tacm.cproducer.Main'

dependencies {

    compile project(":core")

    // logging
    compile group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest {
        attributes 'Main-Class': mainClassName
    }
    archiveBaseName = "clip-producer"
}

task versionTxt() {
    doLast {
        new File("$projectDir/src/main/resources/version.txt").text = """$version-${
            Grgit.open(dir: "$projectDir/..").head().id
        }"""
    }
}

tasks.jar.dependsOn versionTxt