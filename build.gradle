import org.ajoberstar.grgit.Grgit

plugins {
    id 'java'
    id 'application'
    id "org.ajoberstar.grgit" version "1.7.2"
}

version = 1.0

task versionTxt() {
    doLast {
        new File("$projectDir/src/main/resources/version.txt").text = """$version-${
            Grgit.open(dir: "$projectDir").head().id
        }"""
    }
}

mainClassName = 'io.github.sammers21.twitch.Main'

repositories {
    jcenter()
    maven { url "https://oss.jfrog.org/artifactory/libs-release" }
}

dependencies {

    // twitch api 
    compile group: 'com.github.twitch4j', name: 'twitch4j', version: '1.0.0-alpha.13'

    //vertx
    compile group: 'io.vertx', name: 'vertx-core', version: '3.7.1'
    compile group: 'io.vertx', name: 'vertx-web', version: '3.7.1'
    compile group: 'io.vertx', name: 'vertx-web-client', version: '3.7.1'
    compile group: 'io.vertx', name: 'vertx-rx-java2', version: '3.7.1'

    // metrics 
    compile group: 'io.dropwizard.metrics', name: 'metrics-core', version: '4.0.0'
    compile group: 'io.dropwizard.metrics', name: 'metrics-graphite', version: '4.0.0'

    // logging
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.0.9'

    // CLI
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'

    // pg client
    compile 'io.reactiverse:reactive-pg-client:0.11.2'
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest {
        attributes 'Main-Class': mainClassName
    }
    archiveBaseName = "twitch-auto-clip"
}

tasks.build.dependsOn versionTxt